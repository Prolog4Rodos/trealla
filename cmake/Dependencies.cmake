include(CheckLibraryExists)
check_library_exists(m sin "" LIBM_EXISTS)
if(LIBM_EXISTS)
  list(APPEND TPL_LINK_LIBS m)
endif()

if(TPL_USE_ISOCLINE)
  add_compile_definitions(USE_ISOCLINE=1)
elseif(TPL_USE_EDITLINE)
  add_compile_definitions(USE_EDITLINE=1)
  list(APPEND TPL_LINK_LIBS edit)
elseif(TPL_NEEDS_READLINE)
  find_package(PkgConfig QUIET)
  if(PkgConfig_FOUND)
    pkg_check_modules(READLINE QUIET readline)
  endif()

  if(READLINE_FOUND)
    include_directories(${READLINE_INCLUDE_DIRS})
    link_directories(${READLINE_LIBRARY_DIRS})
    list(APPEND TPL_LINK_LIBS ${READLINE_LIBRARIES})
  else()
    list(APPEND TPL_LINK_LIBS readline)
  endif()
endif()

if(TPL_USE_FFI)
  add_compile_definitions(USE_FFI=1)

  find_package(PkgConfig QUIET)
  if(PkgConfig_FOUND)
    pkg_check_modules(FFI QUIET libffi)
  endif()

  if(FFI_FOUND)
    include_directories(${FFI_INCLUDE_DIRS})
    link_directories(${FFI_LIBRARY_DIRS})
    list(APPEND TPL_LINK_LIBS ${FFI_LIBRARIES})
  else()
    find_path(FFI_INCLUDE_DIR ffi.h)
    find_library(FFI_LIBRARY ffi)
    if(NOT FFI_INCLUDE_DIR OR NOT FFI_LIBRARY)
      message(FATAL_ERROR "TPL_USE_FFI=ON but libffi was not found.")
    endif()
    include_directories("${FFI_INCLUDE_DIR}")
    list(APPEND TPL_LINK_LIBS "${FFI_LIBRARY}")
  endif()

  if(UNIX AND NOT APPLE AND NOT TPL_WASI)
    list(APPEND TPL_LINK_LIBS dl)
  endif()
endif()

if(TPL_USE_OPENSSL)
  add_compile_definitions(USE_OPENSSL=1)
  find_package(OpenSSL REQUIRED)
  list(APPEND TPL_LINK_LIBS OpenSSL::SSL OpenSSL::Crypto)
endif()

if(TPL_USE_THREADS)
  add_compile_definitions(USE_THREADS=1)
  set(THREADS_PREFER_PTHREAD_FLAG ON)
  find_package(Threads REQUIRED)
  list(APPEND TPL_LINK_LIBS Threads::Threads)

  if(CMAKE_C_COMPILER_ID STREQUAL "GNU")
    list(APPEND TPL_LINK_LIBS atomic)
  endif()
endif()

if(TPL_LTO)
  include(CheckIPOSupported)
  check_ipo_supported(RESULT _ipo_ok OUTPUT _ipo_msg)
  if(_ipo_ok)
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
  else()
    message(WARNING "IPO/LTO requested but not supported: ${_ipo_msg}")
  endif()
endif()

separate_arguments(_TPL_EXTRA_CFLAGS UNIX_COMMAND "${TPL_EXTRA_CFLAGS}")
separate_arguments(_TPL_EXTRA_LDFLAGS UNIX_COMMAND "${TPL_EXTRA_LDFLAGS}")
